#!/usr/bin/env fish
argparse "h/help" -- $argv
or exit

if set -q _flag_help
	echo "Show the uncompressed and compressed size of files"
	echo "Provide either the names of the files or provide via STDIN"
	echo " -h/--help Show this message"
	exit
end

function human_size
	numfmt --to=iec --suffix=B
end

function show_uncompressed
	wc -c | human_size
end

function measure_compression
	read -lz content
	set -l original_size (echo $content | wc -c)
	set -l compressed_size (echo $content | $argv -c | wc -c)
	set -l compressed_hsize (echo $compressed_size | human_size)
	set -l compressed_percentage (math "ceil(100 * $compressed_size / $original_size)")
	echo "$compressed_hsize ($compressed_percentage%)"
end

if test -d $argv
	read -lz content
	echo "Original" (echo $content | show_uncompressed)
	echo "GZIP" (echo $content | measure_compression gzip)
	echo "Brotli" (echo $content | measure_compression brotli)
else
	for filename in $argv
		echo "$filename" (cat $filename | show_uncompressed)
		echo "$filename.gz" (cat $filename | measure_compression gzip)
		echo "$filename.bz" (cat $filename | measure_compression brotli)
	end
end
