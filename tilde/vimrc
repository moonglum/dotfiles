set nocompatible                            " Use Vim settings, rather then Vi settings
filetype off                                " This is required for Vundle

" === Vundle Setup ===
set rtp+=~/.vim/bundle/Vundle.vim
call vundle#begin()
Plugin 'gmarik/Vundle.vim'
Plugin 'altercation/vim-colors-solarized'

" Polyglot: A collection of language packs, loaded on demand
Plugin 'sheerun/vim-polyglot'
" Fugitive: The best Git integration that exists
Plugin 'tpope/vim-fugitive'
" Commentary: Minimalistic commenting plugin
Plugin 'tpope/vim-commentary'
" Dispatch: asynchronous build and test dispatcher
Plugin 'tpope/vim-dispatch'
" Surround: Quoting/Parenthesizing made simple
Plugin 'tpope/vim-surround'
" Repeat: Enable repeat for Surround and Commentary
Plugin 'tpope/vim-repeat'
" Emoji: Emojis. For Vim. Awesome.
Plugin 'junegunn/vim-emoji'
" Textobj Rubyblock: Custom text object for selecting ruby blocks
Plugin 'kana/vim-textobj-user'
Plugin 'nelstrom/vim-textobj-rubyblock'
" Rainbow: Keep your sanity in a jungle of parentheses
Plugin 'luochen1990/rainbow'
let g:rainbow_active = 1
" Paredit: Port of emacs' paredit to Vim (Only active on Lisps)
Plugin 'paredit.vim'
" Tern: Code-analysis engine for JavaScript
Plugin 'marijnh/tern_for_vim'
" Syntastic: Really great Syntax checker
Plugin 'scrooloose/syntastic'
" Ctrlp: Full path fuzzy finder
Plugin 'kien/ctrlp.vim'
" RSpec: Lightweight RSpec runner
Plugin 'thoughtbot/vim-rspec'

" === Vundle Done ===
call vundle#end()
filetype plugin indent on

" === General Setup ===
syntax enable                               " Activate Syntax Highlight
set encoding=utf-8                          " Set default encoding to UTF-8
set hlsearch                                " Highlight search results
set nowrap                                  " Deactivate Wrapping
set nrformats=                              " Treat all numbers as decimal
set cursorcolumn                            " Highlight the current column
set cursorline                              " Highlight the current line
set noswapfile                              " I don't like Swapfiles
set shell=bash                              " This makes RVM work inside Vim. I have no idea why – Gary Bernhardt
set timeout timeoutlen=1000 ttimeoutlen=100 " Fix slow O inserts from Gary Bernhardt
set ai                                      " Automatically set the indent of a new line
set spell spelllang=en_us                   " Activate spell checking and set American as default
set list listchars=tab:›\ ,trail:‹          " Show invisible characters: Tabs & trailing whitespace
set expandtab                               " Use Soft-Tabs
set tabstop=2 softtabstop=2 shiftwidth=2    " Use two spaces for tabs, soft-tabs and when shifting
set nojoinspaces                            " Insert only one space when joining lines
match ErrorMsg '\%>100v.\+'                 " Highlight Characters that overstep 100 characters

" Jump between pairs of keywords via %
runtime macros/matchit.vim

" === MacVim Setup ===
if has("gui_running")
  set guifont=Source\ Code\ Pro:h14 " Right Font and Font-Size
  set guioptions=egmrt              " No Toolbar
  set guioptions-=r                 " No Scrollbar
endif

" =========== Solarized Dark ===========
" Currently using the dark version
set background=dark
colorscheme solarized

" ======= Syntastic =======
" Use JSHint, not JSLint
let g:syntastic_javascript_checkers = ['jshint']

" Also run a Syntax Check when the file is opened
let g:syntastic_check_on_open=1

" GUI MacVim only: Popover for each line with errors
let g:syntastic_enable_balloons=1

if emoji#available()
  " Terminal Vim for example
  " Bombs and Marks
  " let g:syntastic_error_symbol=emoji#for('boom')
  " let g:syntastic_warning_symbol=emoji#for('exclamation')
  " Cats
  let g:syntastic_error_symbol=emoji#for('scream_cat')
  let g:syntastic_warning_symbol=emoji#for('crying_cat_face')
else
  " GUI MacVim for example
  let g:syntastic_error_symbol='✗'
  let g:syntastic_warning_symbol='⚠'
end

" ======= CtrlP =======
" Ignore files that are git ignored (thanks @railsbros-dirk)
let g:ctrlp_user_command = {
  \ 'types': {
    \ 1: ['.git', 'cd %s && git ls-files --exclude-standard -co'],
    \ 2: ['.ctrlp_root_dir', 'find %s -type f'],
  \ },
  \ 'fallback': 'find %s -type f'
\ }

" Jump to the window when it is already open (only when in this tab)
let g:ctrlp_switch_buffer = 'e'

" Disable caching
let g:ctrlp_use_caching = 0

" ====== rspec.vim ======
let g:rspec_command = "Dispatch rspec {spec}"
let g:rspec_runner = "os_x_iterm"

" === Language Specific Setting ===

autocmd BufRead,BufNewFile *.es6 setfiletype javascript

" Makefiles require tabs
autocmd! FileType make setlocal noexpandtab

" Make sure all markdown files have the correct filetype
autocmd! BufRead,BufNewFile *.{md,mdpp,txt} set ft=markdown

" .rs is Rust, nothing else!
autocmd! BufRead,BufNewFile *.rs set ft=rust
" Community Standard for Rust is indentation of 4
autocmd FileType rust set tabstop=4 softtabstop=4 shiftwidth=4

" Autogroup for prose file types
augroup prose
  autocmd!

  " Use four spaces for tabs, soft-tabs and when shifting
  autocmd FileType markdown set tabstop=4 softtabstop=4 shiftwidth=4

  " Wrap text
  autocmd FileType tex,markdown set wrap linebreak nolist

  " No character limit per line
  autocmd FileType tex,markdown match ErrorMsg ''

  autocmd FileType tex,markdown set nocursorcolumn
augroup END

" === Custom Functions and Commands ===

" Open the current file in Marked
function! s:marked()
  silent exe "!open -a 'Marked 2' %"
  redraw!
endfunction
command! Marked :call s:marked()

" Check current file with JSLint
command! JSLint :Dispatch jslint "%:p"

" Check current file with JSHint
command! JSHint :Dispatch jshint "%:p"

" Switch spell checking language
command! British :set spelllang=en_gb
command! American :set spelllang=en_us
command! German :set spelllang=de

" === Mappings ===

" Clear the search buffer when hitting return
nnoremap <CR> :nohlsearch<cr>
" Directory of current file (`:e %%` for example) – Gary Bernhardt
cnoremap %% <C-R>=expand('%:h').'/'<cr>

" === Leader Key ===

" Execute dispatcher's Make
nnoremap <Leader>m :Make<CR>

" Edit my vimrc (from: Learn Vim Script the hard way)
nnoremap <leader>ev :vne $MYVIMRC<cr>

" Source my vimrc (from: Learn Vim Script the hard way)
nnoremap <leader>sv :source $MYVIMRC<cr>

" Mappings for vim-rspec
nnoremap <leader>t :call RunCurrentSpecFile()<CR>
nnoremap <leader>s :call RunNearestSpec()<CR>
nnoremap <leader>a :call RunAllSpecs()<CR>
